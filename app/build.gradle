apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.template.mvvmtemplate"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    dataBinding {
        enabled true
    }
    buildTypes {
        debug {
            debuggable true
            buildConfigField 'boolean', 'displayErrorMessage', 'true'
            resValue "string", "restBaseUrl", "https://testing.url"
        }
        release {
            debuggable false
            buildConfigField 'boolean', 'displayErrorMessage', 'false'
            resValue "string", "restBaseUrl", "https://production.url"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    supportLibraryVersion = '28.0.0'
    lifecycleVersion = '1.1.1'
    daggerVersion = '2.21'
    retrofitVersion = '2.5.0'
    rxjavaVersion = '2.2.8'
    rxjavaAndroidVersion = '2.1.1'
    okhttpVersion = '3.14.0'
    gsonVersion = '2.8.5'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // SDK
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:1.1.3"
    implementation "com.android.support:multidex:1.0.3"

    // SDK Lifecycle
    implementation "android.arch.lifecycle:extensions:$lifecycleVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$lifecycleVersion"

    // Dagger 2
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"

    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"

    // RxJava v2
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxjavaAndroidVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
